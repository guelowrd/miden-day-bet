# Adds `a` to `b`, wrapping to 128 bits.
export.add              # [bhh bmh bml bll ahh amh aml all ...]
    movup.7             # [all bhh bmh bml bll ahh amh aml ...]
    movup.4             # [bll all bhh bmh bml ahh amh aml ...]
    u32overflowing_add  # [oll rll bhh bmh bml ahh amh aml ...]
    movup.7             # [aml oll rll bhh bmh bml ahh amh ...]
    movup.5             # [bml aml oll rll bhh bmh ahh amh ...]
    u32overflowing_add3 # [oml rml rll bhh bmh ahh amh ...]
    movup.6             # [amh oml rml rll bhh bmh ahh ...]
    movup.5             # [bmh amh oml rml rll bhh ahh ...]
    u32overflowing_add3 # [omh rmh rml rll bhh ahh ...]
    movup.5             # [ahh omh rmh rml rll bhh ...]
    movup.5             # [bhh ahh omh rmh rml rll ...]
    u32wrapping_add3    # [rhh rmh rml rll ...]
end

# Subtracts `b` from `a`, wrapping to 128 bits.
export.sub              # [bhh bmh bml bll ahh amh aml all ...]
    movup.7             # [all bhh bmh bml bll ahh amh aml ...]
    movup.4             # [bll all bhh bmh bml ahh amh aml ...]
    u32overflowing_sub  # [ull rll bhh bmh bml ahh amh aml ...]
    movup.7             # [aml ull rll bhh bmh bml ahh amh ...]
    movup.5             # [bml aml ull rll bhh bmh ahh amh ...]
    u32overflowing_sub  # [uml rml ull rll bhh bmh ahh amh ...]
    swap.1              # [rml uml ull rll bhh bmh ahh amh ...]
    movup.2             # [ull rml uml rll bhh bmh ahh amh ...]
    u32overflowing_sub  # [uml' rml uml rll bhh bmh ahh amh ...]
    movup.2             # [uml uml' rml rll bhh bmh ahh amh ...]
    or                  # [uml rml rll bhh bmh ahh amh ...]
    movup.6             # [amh uml rml rll bhh bmh ahh ...]
    movup.5             # [bmh amh uml rml rll bhh ahh ...]
    u32overflowing_sub  # [umh rmh uml rml rll bhh ahh ...]
    swap.1              # [rmh umh uml rml rll bhh ahh ...]
    movup.2             # [uml rmh umh rml rll bhh ahh ...]
    u32overflowing_sub  # [umh' rmh umh rml rll bhh ahh ...]
    movup.2             # [umh umh' rmh rml rll bhh ahh ...]
    or                  # [umh rmh rml rll bhh ahh ...]
    movup.5             # [ahh umh rmh rml rll bhh ...]
    movup.5             # [bhh ahh umh rmh rml rll ...]
    u32wrapping_sub     # [rhh umh rmh rml rll ...]
    swap.1              # [umh rhh rmh rml rll ...]
    u32wrapping_sub     # [rhh rmh rml rll ...]
end

# Multiplies `a` with `b`, wrapping to 128 bits.
#                 ahh     amh     aml     all
#               x bhh     bmh     bml     bll
# -------------------------------------------
#                                ovfl allxbll
#                        ovfl amlxbll       0
#                ovfl amhxbll       0       0
#             ahhxbll       0       0       0     bll x4 uses, ahh x1 uses
#                        ovfl allxbml       0
#                ovfl amlxbml       0       0
#             amhxbml       0       0       0     amh x2 uses, bml x3 uses
#                ovfl allxbmh       0       0
#             amlxbmh       0       0       0     aml x3 uses, bmh x2 uses
#             allxbhh       0       0       0     all x4 uses, bhh x1 uses
# -------------------------------------------
#                  rhh    rmh     rml     rll

export.mul              # [bhh bmh bml bll ahh amh aml all ... ]
    dup.7               # [all bhh bmh bml bll ahh amh aml all ... ]
    dup.4               # [bll all bhh bmh bml bll ahh amh aml all ... ]
    u32overflowing_mul  # [o rll bhh bmh bml bll ahh amh aml all ... ]
    dup.8               # [aml o rll bhh bmh bml bll ahh amh aml all ... ]
    dup.6               # [bll aml o rll bhh bmh bml bll ahh amh aml all ... ]
    u32overflowing_madd # [o rml' rll bhh bmh bml bll ahh amh aml all ... ]
    dup.8               # [amh o rml' rll bhh bmh bml bll ahh amh aml all ... ]
    dup.7               # [bll amh o rml' rll bhh bmh bml bll ahh amh aml all ... ]
    u32overflowing_madd # [o rmh' rml' rll bhh bmh bml bll ahh amh aml all ... ]
    movup.8             # [ahh o rmh' rml' rll bhh bmh bml bll amh aml all ... ]
    movup.8             # [bll ahh o rmh' rml' rll bhh bmh bml amh aml all ... ]
    u32wrapping_madd    # [rhh' rmh' rml' rll bhh bmh bml amh aml all ... ]
    movup.2             # [rml' rhh' rmh' rll bhh bmh bml amh aml all ... ]
    dup.9               # [all rml' rhh' rmh' rll bhh bmh bml amh aml all ... ]
    dup.7               # [bml all rml' rhh' rmh' rll bhh bmh bml amh aml all ... ]
    u32overflowing_madd # [o rml rhh' rmh' rll bhh bmh bml amh aml all ... ]
    dup.9               # [aml o rml rhh' rmh' rll bhh bmh bml amh aml all ... ]
    dup.8               # [bml aml o rml rhh' rmh' rll bhh bmh bml amh aml all ... ]
    u32overflowing_madd # [o rmh' rml rhh' rmh' rll bhh bmh bml amh aml all ... ]
    movup.9             # [amh o rmh' rml rhh' rmh' rll bhh bmh bml aml all ... ]
    movup.9             # [bml amh o rmh' rml rhh' rmh' rll bhh bmh aml all ... ]
    u32wrapping_madd    # [rhh' rmh' rml rhh' rmh' rll bhh bmh aml all ... ]
    swap.1              # [rmh' rhh' rml rhh' rmh' rll bhh bmh aml all ... ]
    dup.9               # [all rmh' rhh' rml rhh' rmh' rll bhh bmh aml all ... ]
    dup.8               # [bmh all rmh' rhh' rml rhh' rmh' rll bhh bmh aml all ... ]
    u32overflowing_madd # [o rmh' rhh' rml rhh' rmh' rll bhh bmh aml all ... ]
    movup.9             # [aml o rmh' rhh' rml rhh' rmh' rll bhh bmh all ... ]
    movup.9             # [bmh aml o rmh' rhh' rml rhh' rmh' rll bhh all ... ]
    u32wrapping_madd    # [rhh' rmh' rhh' rml rhh' rmh' rll bhh all ... ]
    movup.8             # [all rhh' rmh' rhh' rml rhh' rmh' rll bhh ... ]
    movup.8             # [bhh all rhh' rmh' rhh' rml rhh' rmh' rll ... ]
    u32wrapping_madd    # [rhh' rmh' rhh' rml rhh' rmh' rll ... ]
    swap.1              # [rmh' rhh' rhh' rml rhh' rmh' rll ... ]
    movup.5             # [rmh' rmh' rhh' rhh' rml rhh' rll ... ]
    u32overflowing_add  # [o rmh rhh' rhh' rml rhh' rll ... ]
    movup.5             # [rhh' o rmh rhh' rhh' rml rll ... ]
    movup.3             # [rhh' rhh' o rmh rhh' rml rll ... ]
    u32wrapping_add3    # [rhh' rmh rhh' rml rll ... ]
    movup.2             # [rhh' rhh' rmh rml rll ... ]
    u32wrapping_add     # [rhh rmh rml rll ... ]
end
